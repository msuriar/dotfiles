# Options enabling hooks and substitutions
typeset -ga preexec_functions
typeset -ga precmd_functions
typeset -ga chpwd_functions
setopt promptsubst

if [[ -n $SSH_CLIENT ]]; then
	HOSTNAMECOLOUR='cyan'
else
	HOSTNAMECOLOUR='green'
fi

# Git stuff.
export __CURRENT_GIT_BRANCH=
export __CURRENT_GIT_STATUS=

parse_git_branch() {
	git branch --no-color 2> /dev/null \
	| grep '^\*' | sed -e 's/^\* \(.*\)/%{%F{cyan}%}git:\1%{%f%}/'
}

get_git_prompt_info() {
  if [[ -n ${__CURRENT_GIT_BRANCH} ]] ; then
    if [[ -n ${__CURRENT_GIT_COMMIT_TIME} ]]; then
      echo "(${__CURRENT_GIT_BRANCH}|$__CURRENT_GIT_COMMIT_TIME)"
    else
      echo "($__CURRENT_GIT_BRANCH)"
    fi
  else
    echo ""
  fi
}

get_git_time_since_commit() {
  LAST_COMMIT_TIME=$(git log -1 --pretty=format:%at)
  (( TIME_SINCE_COMMIT = $(date +%s) - ${LAST_COMMIT_TIME} ))
  if [[ ${TIME_SINCE_COMMIT} -le 600 ]]; then
    TIMECOLOR="green"
    TIME="$((${TIME_SINCE_COMMIT}/60))m"
  elif [[ ${TIME_SINCE_COMMIT} -le 3600 ]]; then 
    TIMECOLOR="yellow"
    TIME="$((${TIME_SINCE_COMMIT}/60))m"
  elif [[ ${TIME_SINCE_COMMIT} -le 84600 ]]; then
    TIMECOLOR="red"
    TIME="$((${TIME_SINCE_COMMIT}/3600))h"
  else
    TIMECOLOR="red"
    TIME="$((${TIME_SINCE_COMMIT}/84600))D"
  fi
  export __CURRENT_GIT_COMMIT_TIME="%{%F{${TIMECOLOR}}%}${TIME}%{%f%}"
}

zsh_update_git_vars() {
  export __CURRENT_GIT_BRANCH="$(parse_git_branch)"
  if [[ -n ${__CURRENT_GIT_BRANCH} ]]; then
    export __CURRENT_GIT_STATUS="$(git status --short)"
    if [[ -n ${__CURRENT_GIT_STATUS} ]]; then
      get_git_time_since_commit
    else
      unset __CURRENT_GIT_COMMIT_TIME
    fi
  else
    unset __CURRENT_GIT_BRANCH __CURRENT_GIT_STATUS __CURRENT_GIT_COMMIT_TIME
  fi
}

# HG stuff.
export __CURRENT_HG_BRANCH=

parse_hg_branch() {
	hg branch 2> /dev/null \
	| sed -e 's/^\(.*\)$/(%{%F{cyan}%}hg:\1%{%f%})/'
}

get_hg_prompt_info() {
	echo $__CURRENT_HG_BRANCH
}

# Update VCS variables
zsh_update_vcs_vars() {
  zsh_update_git_vars
  export __CURRENT_HG_BRANCH="$(parse_hg_branch)"
}

# Preexec/chpwd/precmd functions for VCS info.
preexec_functions+='zsh_update_vcs_vars'
chpwd_functions+='zsh_update_vcs_vars'
precmd_functions+='zsh_update_vcs_vars'

PROMPT='[%{%F{yellow}%}%n%{%f%}@\
%{%F{$HOSTNAMECOLOUR}%}%2m%{%f%}:\
%{%F{blue}%}%~%{%f%}]\
%(?..[%{%F{red}%}%?%{%f%}])\
$(get_git_prompt_info)\
$(get_hg_prompt_info)
%# '

export PROMPT RPROMPT
