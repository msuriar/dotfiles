# Options enabling hooks and substitutions
typeset -ga preexec_functions
typeset -ga precmd_functions
typeset -ga chpwd_functions
setopt promptsubst

if [[ -n $SSH_CLIENT ]]; then
	HOSTNAMECOLOUR='cyan'
else
	HOSTNAMECOLOUR='green'
fi

# Git stuff.
export __CURRENT_GIT_BRANCH=

parse_git_branch() {
	git branch --no-color 2> /dev/null \
	| grep '^\*' | sed -e 's/^\* \(.*\)/(%F{red}git:\1%f)/'
}

preexec_functions+='zsh_preexec_update_git_vars'
zsh_preexec_update_git_vars() {
	case "$(history $HISTCMD)" in
		*git*)
		export __CURRENT_GIT_BRANCH="$(parse_git_branch)"
		;;
	esac
}

chpwd_functions+='zsh_chpwd_update_git_vars'
zsh_chpwd_update_git_vars() {
	export __CURRENT_GIT_BRANCH="$(parse_git_branch)"
}

get_git_prompt_info() {
	echo $__CURRENT_GIT_BRANCH
}

# HG stuff.
export __CURRENT_HG_BRANCH=

parse_hg_branch() {
	hg branch 2> /dev/null \
	| sed -e 's/^\(.*\)$/(%F{red}hg:\1%f)/'
}

preexec_functions+='zsh_preexec_update_hg_vars'
zsh_preexec_update_hg_vars() {
	case "$(history $HISTCMD)" in
		*hg*)
		export __CURRENT_HG_BRANCH="$(parse_hg_branch)"
		;;
	esac
}

chpwd_functions+='zsh_chpwd_update_hg_vars'
zsh_chpwd_update_hg_vars() {
	export __CURRENT_HG_BRANCH="$(parse_hg_branch)"
}

get_hg_prompt_info() {
	echo $__CURRENT_HG_BRANCH
}

PROMPT='%{[%F{yellow}%n%f@%F{$HOSTNAMECOLOUR}%m%f:%F{blue}%~%f]%}$(get_git_prompt_info)$(get_hg_prompt_info)
%# '

export PROMPT RPROMPT
