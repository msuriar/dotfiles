# Options enabling hooks and substitutions
typeset -ga preexec_functions
typeset -ga precmd_functions
typeset -ga chpwd_functions
setopt promptsubst

if [[ -n $SSH_CLIENT ]]; then
	HOSTNAMECOLOUR='cyan'
else
	HOSTNAMECOLOUR='green'
fi

# Git stuff.
export __CURRENT_GIT_BRANCH=

parse_git_branch() {
	git branch --no-color 2> /dev/null \
	| grep '^\*' | sed -e 's/^\* \(.*\)/(%{%F{red}%}git:\1%{%f%})/'
}

get_git_prompt_info() {
	echo $__CURRENT_GIT_BRANCH
}

# HG stuff.
export __CURRENT_HG_BRANCH=

parse_hg_branch() {
	hg branch 2> /dev/null \
	| sed -e 's/^\(.*\)$/(%{%F{red}%}hg:\1%{%f%})/'
}

get_hg_prompt_info() {
	echo $__CURRENT_HG_BRANCH
}

# Update VCS variables
zsh_update_vcs_vars() {
  export __CURRENT_GIT_BRANCH="$(parse_git_branch)"
  export __CURRENT_HG_BRANCH="$(parse_hg_branch)"
}

# Preexec/chpwd/precmd functions for VCS info.
preexec_functions+='zsh_update_vcs_vars'
chpwd_functions+='zsh_update_vcs_vars'
precmd_functions+='zsh_update_vcs_vars'

PROMPT='[%{%F{yellow}%}%n%{%f%}@%{%F{$HOSTNAMECOLOUR}%}%m%{%f%}:%{%F{blue}%}%~%{%f%}]%(?..[%{%F{red}%}%?%{%f%}])$(get_git_prompt_info)$(get_hg_prompt_info)
%# '

export PROMPT RPROMPT
