# Options enabling hooks and substitutions
typeset -ga preexec_functions
typeset -ga precmd_functions
typeset -ga chpwd_functions
setopt promptsubst

. ${ZCONFDIR}/zsh-prompt.git
. ${ZCONFDIR}/zsh-prompt.hg

get_prompt_from_commit_time() {
  local commit_time=$1

  (( time_since_commit = $(date +%s) - ${commit_time} ))
  if [[ ${time_since_commit} -le 600 ]]; then
    time_color="green"
    time_since="$((${time_since_commit}/60))m"
  elif [[ ${time_since_commit} -le 3600 ]]; then
    time_color="yellow"
    time_since="$((${time_since_commit}/60))m"
  elif [[ ${time_since_commit} -le 84600 ]]; then
    time_color="red"
    time_since="$((${time_since_commit}/3600))h"
  else
    time_color="red"
    time_since="$((${time_since_commit}/84600))d"
  fi
  echo "%{%F{${time_color}}%}${time_since}%{%f%}"
}


# Update VCS variables
zsh_update_vcs_vars() {
  zsh_update_git_vars
  zsh_update_hg_vars
}

# Preexec/chpwd/precmd functions for VCS info.
preexec_functions+='zsh_update_vcs_vars'
chpwd_functions+='zsh_update_vcs_vars'
precmd_functions+='zsh_update_vcs_vars'

if [[ -n $SSH_CLIENT ]]; then
	HOSTNAMECOLOUR='cyan'
else
	HOSTNAMECOLOUR='green'
fi

p_user='%{%F{yellow}%}%n%{%f%}'
p_host='%{%F{$HOSTNAMECOLOUR}%}%m%{%f%}'
p_pwd='%{%F{blue}%}%~%{%f%}'
p_exit='%(?..[%{%F{red}%}%?%{%f%}])'

base_prompt="[${p_user}@${p_host}:${p_pwd}]${p_exit}"
vcs_prompt='$(get_git_prompt_info)$(get_hg_prompt_info)'

PROMPT="${base_prompt}${vcs_prompt}
%# "

export PROMPT RPROMPT
