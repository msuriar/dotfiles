. ${ZCONFDIR}/zsh-prompt.git
. ${ZCONFDIR}/zsh-prompt.hg

get_prompt_from_commit_time() {
  local commit_time=$1

  (( time_since_commit = $(date +%s) - ${commit_time} ))

  case ${time_since_commit} in
    <-600>)
      time_color="green"
      time_since="$((${time_since_commit}/60))m"
    ;;
    <601-3600>)
      time_color="yellow"
      time_since="$((${time_since_commit}/60))m"
    ;;
    <3601-84600>)
      time_color="red"
      time_since="$((${time_since_commit}/60/60))h"
    ;;
    <84601-31536000>)
      time_color="red"
      time_since="$((${time_since_commit}/60/60/24))d"
    ;;
    <31536001->)
      time_color="red"
      time_since="$((${time_since_commit}/60/60/24/365))y"
    ;;
  esac
  echo "%{%F{${time_color}}%}${time_since}%{%f%}"
}

get_vcs_prompt_info() {
  if in_git_repo ; then
    branch_name="%{%F{cyan}%}git:${VCS_BRANCH}%{%f%}"
    if [[ -v VCS_REPO_DIRTY ]] ; then
      commit_delay=$(get_prompt_from_commit_time ${VCS_LAST_COMMIT_TIME})
      echo "(${branch_name}|${commit_delay})"
    else
      echo "(${branch_name})"
    fi
  elif in_hg_repo ; then
    if [[ -v VCS_BRANCH ]]; then
      branch_name="%{%F{cyan}%}hg:${VCS_BRANCH}%{%f%}"
    else
      branch_name="%{%F{cyan}%}hg%{%f%}"
    fi
    if [[ -v VCS_REPO_DIRTY ]] ; then
      commit_delay=$(get_prompt_from_commit_time ${VCS_LAST_COMMIT_TIME})
      echo "(${branch_name}|${commit_delay})"
    else
      echo "(${branch_name})"
    fi
  else
    echo ""
  fi
}

zsh_clear_vcs_vars() {
  unset VCS_BRANCH
  unset VCS_LAST_COMMIT_TIME
  unset VCS_REPO_DIRTY
}

zsh_update_vcs_vars() {
  zsh_clear_vcs_vars
  if in_git_repo; then
    zsh_update_git_params
  elif in_hg_repo; then
    zsh_update_hg_params
  fi
}

